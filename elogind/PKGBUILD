# Maintainer: artoo <artoo@cromnix.org>

pkgbase=elogind
pkgname=('elogind' 'libelogind')
pkgver=234.4
pkgrel=5
pkgdesc="The systemd project's logind, extracted to a standalone package"
arch=('i686' 'x86_64' 'armv7h')
url="https://github.com/elogind/elogind"
license=('GPL' 'LGPL2.1')
makedepends=('intltool' 'libtool' 'gperf' 'gtk-doc' 'polkit' 'dbus' 'eudev' 'libseccomp')
options=('!libtool')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/elogind/elogind/archive/v${pkgver}.tar.gz"
        "protect-user-XDG_RUNTIME_DIR.patch")

sha256sums=('ffb465a62c4281c19b07eb7fbb9ad0987f3b726ae1a868a92a3144d7714542f3'
            '4b04eb4bd87a6b0b15290f87b475d87920d5060ee7bf1583a2320f8f7f4c7658')

prepare() {
    cd ${pkgbase}-${pkgver}
    patch -Np1 -i ${srcdir}/protect-user-XDG_RUNTIME_DIR.patch
    ./autogen.sh
}

build() {
    cd ${pkgbase}-${pkgver}
    intltoolize
    ./configure \
        --sysconfdir=/etc \
        --prefix=/usr \
        --libdir=/usr/lib \
        --libexecdir=/usr/lib \
        --enable-split-usr \
        --enable-polkit \
        --disable-smack \
        --enable-acl \
        --enable-pam \
        --with-rootlibdir=/usr/lib \
        --with-udevrulesdir=/usr/lib/udev/rules.d \
        --with-rootlibexecdir=/usr/lib/elogind \
        --with-cgroup-controller=openrc \
        --without-kill-user-processes
    make
}

package_elogind() {
    pkgdesc="The systemd project's logind, extracted to a standalone package"
    provides=("elogind=${pkgver}")
    depends=('acl' 'pam' 'dbus' 'eudev' 'libseccomp')
    conflicts=('consolekit')
    optdepends=('elogind-openrc: elogind initscript'
                'polkit: polkit support')

    cd ${pkgbase}-${pkgver}
    make DESTDIR="${pkgdir}" install

    mkdir ${srcdir}/_libelogind
    mv -v ${pkgdir}/usr/lib/pkgconfig ${srcdir}/_libelogind
    mv -v ${pkgdir}/usr/include/ ${srcdir}/_libelogind
    mv -v ${pkgdir}/usr/lib/libelogind*.so* ${srcdir}/_libelogind
}

package_libelogind(){
    pkgdesc="elogind client libraries"
    groups=('base-devel')
    provides=('libelogind.so' "libelogind=${pkgver}")
    depends=('libcap')

    cd ${pkgbase}-${pkgver}

    install -dm755 ${pkgdir}/usr/lib
    mv ${srcdir}/_libelogind/include ${pkgdir}/usr
    mv ${srcdir}/_libelogind/pkgconfig ${pkgdir}/usr/lib
    mv ${srcdir}/_libelogind/libelogind*.so* ${pkgdir}/usr/lib
}
