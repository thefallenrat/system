# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=libseccomp
pkgver=2.3.2
pkgrel=2
pkgdesc='Enhanced seccomp library'
arch=('x86_64')
license=('LGPL2.1')
url="https://github.com/seccomp/libseccomp"
depends=('glibc')
validpgpkeys=('7100AADFAE6E6E940D2E0AD655E45A5AE8CA7C8A') # Paul Moore <paul@paul-moore.com>
source=("https://github.com/seccomp/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        '0001-arch-add-the-statx-syscall.patch')
sha256sums=('3ddc8c037956c0a5ac19664ece4194743f59e1ccd4adde848f4f0dae7f77bca1'
            'SKIP'
            '190c6dd821ddad9ee0aefa720a86dd744acd279c095b07f099a7e0e72d1ce543')

prepare() {
  cd $pkgname-$pkgver

  # arch: add the statx syscall
  patch -Np1 < "${srcdir}"/0001-arch-add-the-statx-syscall.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
