# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libtirpc
pkgver=1.0.2
pkgrel=2
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=('x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
depends=('krb5')
backup=('etc/netconfig')
# git tree: git://linux-nfs.org/~steved/libtirpc
source=(http://downloads.sourceforge.net/sourceforge/libtirpc/${pkgname}-${pkgver}.tar.bz2 buildfix.diff)
sha1sums=('2a8dc0e6eecc45be6597c8287b1d8e15cbee46e3'
          '6bf8a0afc9f11832f94573ff938114aad2cfa862')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i $srcdir/buildfix.diff
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconf=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
